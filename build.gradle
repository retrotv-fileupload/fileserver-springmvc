plugins {
	id 'java'
	id 'jacoco'
	id 'org.springframework.boot' version '3.5.7'
	id 'io.spring.dependency-management' version '1.1.7'
}

group = 'dev.retrotv'
version = '0.0.1-SNAPSHOT'
description = 'Demo project for Spring Boot'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

configurations {
	configureEach {
		exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
	}
}

dependencies {
	implementation 'dev.retrotv:framework.foundation.common:0.0.34-alpha'

	// UUID 라이브러리
	implementation 'com.github.f4b6a3:uuid-creator:5.3.5'

	// SQLite3용 JDBC
	implementation 'org.xerial:sqlite-jdbc:3.45.2.0'

	// SQLite3용 Dialect
	implementation 'org.hibernate.orm:hibernate-community-dialects:6.6.26.Final'

	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

	// Tomcat 제외, Undertow 사용
	implementation ('org.springframework.boot:spring-boot-starter-web') {
		exclude group: 'org.springframework.boot', module: 'spring-boot-starter-tomcat' // Tomcat 제외
	}
	implementation 'org.springframework.boot:spring-boot-starter-undertow'
	implementation "org.springframework.boot:spring-boot-starter-log4j2";

	compileOnly 'org.projectlombok:lombok'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
	useJUnitPlatform()
	finalizedBy jacocoTestReport
}

jacocoTestReport {
    reports {
        xml.required = true
        xml.outputLocation = layout.buildDirectory.dir('jacoco/jacoco.xml').get().asFile
    }

    dependsOn test

	afterEvaluate {
        classDirectories.setFrom(
            files(
				classDirectories.files.collect {
					fileTree(dir: it, exclude: [
						'**/dtos/**',
						'**/entities/**',
						'**/enums/**',
						'**/common/**',
						'**/config/**',
						'**/FileserverApplication.*'
					])
				}
			)
        )
    }
}
